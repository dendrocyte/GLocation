apply plugin: 'maven-publish'
apply plugin: 'signing'

project.afterEvaluate {
    publishing {
        publications {
            /**
             * 下行的myLibrary是指在publication夾子的新增的子夾名
             * MavenPublication 是指遠端目的地
             * NOTE: 和snapshot 使用不同的publication子夾
             */
            myLibrary(MavenPublication) {
                /**
                 * 在 build/publication/myLibrary
                 * 生成 pom.xml
                 * 並註明此library的groupId, artifactId, version
                 *
                 *
                 * 在其他專案中被導入時則使用
                 * implementation "com.dendrocyte:haylocation:1.0"
                 */
                groupId githubPackageGroup
                artifactId publishArtifactId
                //NOTE: 沒有-SNATSHOT字尾
                version publishVersion
                from components.release
                //NOTE: 要複製 SNAPSHOT's pom
                pom{
                    name = publishName
                    description = publishDesc
                    url = websiteUrl
                    licenses {
                        license {
                            name = licenseName
                            url = licenseUrl
                        }
                    }
                    // Specify developer information
                    developers {
                        developer {
                            id = devName
                            name = devName
                            email = devEmail
                        }
                    }
                    // Specify SCM information
                    scm {
                        url = websiteUrl
                    }
                }
            }
        }
        repositories {
            maven {
                name = "GitHubPackages"
                url = "https://maven.pkg.github.com/${githubUser}/${publishName}"
                credentials {
                    //gradle.properties ?: env
                    // (= publish via task ?: via workflow)
                    username = project.findProperty("gpr.user") ?: System.getenv("GITHUB_ACTOR")
                    //NOTE: 使用不同的pwd 上傳
                    password = project.findProperty("gpr.password") ?: System.getenv("GITHUB_TOKEN")
                }
            }
        }
    }
    signing {
        // myLibrary 是publications的子夾名字，可以指定sign哪一個夾子
        sign publishing.publications.myLibrary
    }
}









